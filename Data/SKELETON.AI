define SKELETON         5120
define SKELETONSTRENGTH 100
define DOOT             27
define SKELETON_HIT     31
define SKELETON_DIE     32
define SKELETON_SLAP    44
define BONE1            5157
define BONE2            5158
define BONE3            5159

definesound DOOT                   DOOT.WAV        0   0    3   0   512
definesound SKELETON_HIT           SKELEHIT.WAV    0   0    3   0   512
definesound SKELETON_DIE           SKELEDIE.WAV    0   0    3   0   512
definesound SKELETON_SLAP          SKELSLAP.WAV    0   0    3   0   512

action ASKELETONIDLE    0 1 1 1
action ASKELETONWALK    0 4 5 1 10
action ASKELETONATTACK 25 5 1 1 10
action ASKELETONDIE    30 7 1 1 10
action ASKELETONDOOT   40 2 5 1 5
action ASKELETONDEAD   36 1 1 1 1
action ASKELETONFROZEN  0 1 5
action ASKELETONGROW    0

move SKELETONRUNVELS 120
move SKELETONSTOPPED

ai AISKELETONIDLE         ASKELETONIDLE   SKELETONSTOPPED
ai AISKELETONSEEKENEMY    ASKELETONWALK   SKELETONRUNVELS seekplayer  geth
ai AISKELETONCHARGEENEMY  ASKELETONWALK   SKELETONRUNVELS faceplayer  geth
ai AISKELETONATTACK       ASKELETONATTACK SKELETONSTOPPED faceplayer
ai AISKELETONDODGE        ASKELETONWALK   SKELETONRUNVELS dodgebullet geth
ai AISKELETONDOOT         ASKELETONDOOT   SKELETONSTOPPED faceplayer
ai AISKELETONSHRINK       ASKELETONWALK   SHRUNKVELS      fleeenemy
ai AISKELETONGROW         ASKELETONGROW   SKELETONSTOPPED faceplayerslow
ai AISKELETONDYING        ASKELETONDIE    SKELETONSTOPPED faceplayer

state bonestate
  sizeat 10 10

  fall

  ifaction 0
  {
    action ANULLACTION
  }

  ifhitweapon
  {
    ifdead
    {
      ifrnd 128
      {
        sound SKELETON_HIT
      }

      ifrnd 128
      {
        debris SCRAP1 2
      }

      ifrnd 96
      {
        debris SCRAP2 1
      }

      ifrnd 64
      {
        debris SCRAP3 2
      }

      killit
    }
  }
ends

useractor notenemy BONE1 WEAK
  state bonestate
enda

useractor notenemy BONE2 WEAK
  state bonestate
enda

useractor notenemy BONE3 WEAK
  state bonestate
enda

state bone_jibs
  ifrnd 64
  {
    spawn BONE1
  }
  else
    ifrnd 48
  {
    spawn BONE2
  }
  else
    ifrnd 32
  {
    spawn BONE3
  }
ends

useractor enemy SKELETON SKELETONSTRENGTH ASKELETONIDLE
  fall

  state checksquished

  ifaction ASKELETONIDLE
  {
    cstat 257
    ai AISKELETONSEEKENEMY
  }
  else
    ifaction ASKELETONDEAD
  {
    ifrespawn
      ifcount RESPAWNACTORTIME
    {
      spawn TRANSPORTERSTAR
      cstat 257
      strength SKELETONSTRENGTH
      ai AISKELETONSEEKENEMY
    }
    else
    {
      strength 0

      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        state bone_jibs
        sound SQUISHED
        killit
      }
      break
    }
  }
  else
    ifaction ASKELETONFROZEN
  {
    ifcount THAWTIME
    {
      ai AISKELETONSEEKENEMY
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
        ifrnd 8
    {
      spawn WATERDRIP
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
        strength 0
        break
      }

// TODO: spawn bones with frozen pal

      addkills 1
      state bone_jibs
      lotsofglass 30
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
    {
        pkick
    }

    break
  }
  else
    ifai AISKELETONDYING
  {
    ifactioncount 7
    {
      state rf

      state bone_jibs
      sound SKELETON_HIT

      iffloordistl 8
      {
        sound THUD
      }

      action ASKELETONDEAD
      move SKELETONSTOPPED

      break
    }
  }
/*
  else
    ifai ASKELETONHIT
  {
    ifactioncount 3
    {
      ai AISKELETONSEEKENEMY
    }
  }
*/
  else
    ifai AISKELETONSHRINK
  {
    ifcount SHRUNKDONECOUNT
    {
      ai AISKELETONSEEKENEMY
    }
    else
      ifcount SHRUNKCOUNT
    {
        sizeto 40 40
    }
    else
    {
      state genericshrunkcode
    }
  }
  else
    ifai AISKELETONGROW
  {
    ifcount 32
    {
      debris SCRAP1 2
      debris SCRAP2 3
      debris SCRAP3 1
      addkills 1
      sound SQUISHED
      sound PIPEBOMB_EXPLODE
      hitradius 2048 60 70 80 90
      spawn EXPLOSION2
      killit
    }
    else
    {
      sizeto 200 200
      break
    }
  }
  else
  {
    ifai AISKELETONSEEKENEMY
      ifactornotstayput
        ifp palive
          ifcansee
    {
      ai AISKELETONCHARGEENEMY
    }
    else
      ifai AISKELETONCHARGEENEMY
        ifactornotstayput
          ifp palive
            ifcansee
              ifpdistl 1024
    {
      ai AISKELETONATTACK
    }
    else
      ifai AISKELETONATTACK
        ifactornotstayput
    {
      ifcansee
      {
        ifpdistl 1024
        {
          ifcount 15
          {
            resetcount
          }
          else
            ifcount 6
          {
              nullop
          }
          else
            ifcount 5
          {
            palfrom 8 32
            addphealth -10
            sound SKELETON_SLAP

            ifphealthl 30
            {
              ifrnd 192
              {
                wackplayer
              }
            }
            else
              ifphealthl 50
            {
              ifrnd 128
              {
                wackplayer
              }
            }
            else
            {
              ifrnd 64
              {
                wackplayer
              }
            }
          }
        }
        else
        {
          ai AISKELETONCHARGEENEMY
        }
      }
      else
      {
        ai AISKELETONSEEKENEMY
      }
    }

// TODO: missing stuff

    ifhitweapon
    {
      state bone_jibs

      ifdead
      {
        ifwasweapon GROWSPARK
        {
          sound ACTOR_GROWING
          ai AISKELETONGROW
          break
        }

        addkills 1

        ifwasweapon FREEZEBLAST
        {
          sound SOMETHINGFROZE
          spritepal 1
          move 0
          action ASKELETONFROZEN
          strength 0
          break
        }

        ifwasweapon RADIUSEXPLOSION
        {
          sound SQUISHED
          state bone_jibs
          killit
        }
        else
          ifwasweapon RPG
        {
          sound SQUISHED
          state bone_jibs
          killit
        }
        else
        {
          ai AISKELETONDYING
          sound SKELETON_DIE
        }
      }
      else
      {
        state bone_jibs
        sound SKELETON_HIT

        ifwasweapon SHRINKSPARK
        {
          sound ACTOR_SHRINKING
          ai AISKELETONSHRINK
        }
        else
          ifwasweapon GROWSPARK
        {
          sound EXPANDERHIT
        }
        else
        {
// TODO: do something in response to being attacked
        }
      }
    }
  }

// add chance to respawn
// add bone sounds
// add death animation
// add trumpet
// add normal attack sound
// add random chance to do some special attack
// add custom palette versions?
// see minecraft mod
// fix shrinker effect
// 

/*
  ifbulletnear
  {
    ai AISKELETONDODGE
  }

  ifai AISKELETONSEEKENEMY
    ifactornotstayput
      ifp palive
        ifcansee
  {
    ai AISKELETONCHARGEENEMY
  }
  else
    ifai AISKELETONCHARGEENEMY
      ifactornotstayput
        ifp palive
          ifcansee
            ifpdistl 1024
  {
    ai AISKELETONATTACK
  }
  else
    ifai AISKELETONATTACK
      ifactornotstayput
  {
    ifcansee
    {
      ifpdistl 1024
        nullop
      else
        ai AISKELETONCHARGEENEMY
    }
    else
    {
      ai AISKELETONSEEKENEMY
    }
  }
  else
    ifai AISKELETONDOOT
      ifcount 64
  {
    sound DOOT
    ai AISKELETONSEEKENEMY
    resetcount
  }
*/
enda
